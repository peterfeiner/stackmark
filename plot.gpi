#!/usr/bin/env gnuplot

atop_file = TITLE . '.atop'
phases_file = TITLE . '.phases'

set macros
#set term svg size 800, 300
set term pngcairo size 600, 250 font 'Arial,10'
set output TITLE . '.png'
set style data lines

N = `echo @TITLE | sed 's/@.*//' | sed 's/.*-//'`

#DIRNAME = "`dirname $TITLE`"
#DIRNAME = "`basename $DIRNAME`"

DIRNAME = system("basename " . system("dirname " . TITLE))

set title DIRNAME . ' N=' . N
set xlabel 'time (s)'
set ylabel ''
set yrange [0:1]
LAST = `tail -n1 @TITLE.phases | awk '{print $1}'`
set xrange [0:LAST]
set xtics

set key outside
set rmargin 20


start_time = `atop -r @atop_file  -PCPU | grep CPU | head -n 1 | awk '{print $3}'`

utilization(total, idle) = (total - idle) / total

cpu_cmd = '< atop -r ' . TITLE . '.atop -PCPU | sed -n "0,/^SEP$/d;p" | grep -v SEP'
dsk_cmd = '< atop -r ' . TITLE . '.atop -PDSK | sed -n "0,/^SEP$/d;p" | grep -v SEP | grep sda'
boot_cmd = '< grep "\<in-boot\>" ' . TITLE . '.phases'
ping_cmd = '< grep "\<in-ping\>" ' . TITLE . '.phases'
ssh_cmd = '< grep "\<in-ssh\>" ' . TITLE . '.phases'
delete_cmd = '< grep "\<in-delete\>" ' . TITLE . '.phases'

merge_cmd = '< ./merge-phases.py ' . TITLE . '.phases boot_api boot ping ssh delete_api delete fin'


#set style line 8 lt 1 lw 2 pt 0 linecolor rgb "#d77fb3"
set style line 7 lt 1 lw 2 pt 0 linecolor rgb "#4CEAFC"
set style line 6 lt 1 lw 2 pt 0 linecolor rgb "#7C40D6"
set style line 5 lt 1 lw 1 pt 0 linecolor rgb "#e8a55a"
set style line 4 lt 1 lw 1 pt 0 linecolor rgb "#599ad3"
set style line 3 lt 1 lw 1 pt 0 linecolor rgb "#79c36a"
set style line 2 lt 1 lw 1 pt 0 linecolor rgb "#dc545e"
set style line 1 lt 1 lw 1 pt 0 linecolor rgb "#909090"

plot \
     merge_cmd \
     using ($1):(($2 + $3 + $4 + $5 + $6 + $7 + $8) / N) \
     title 'deleted / N' with boxes fs solid ls 1, \
     "" \
     using ($1):(($2 + $3 + $4 + $5 + $6 + $7) / N) \
     title 'deleting / N' with boxes fs solid ls 2, \
     "" \
     using ($1):(($2 + $3 + $4 + $5) / N) \
     title 'sshing / N' with boxes fs solid ls 3, \
     "" \
     using ($1):(($2 + $3 + $4) / N) \
     title 'pinging / N' with boxes fs solid ls 4, \
     "" \
     using ($1):(($2 + $3) / N) \
     title 'creating / N' with boxes fs solid ls 5, \
     cpu_cmd \
     using ($3 - start_time):(utilization($6 * $7 * $8, $12)) \
     title 'U(CPU)' ls 6, \
     dsk_cmd \
     using ($3 - start_time):($8 / ($6 * 1000)) \
     title 'U(DSK)' ls 7, \
     '< true' using 1:2
